exists() { type "$1" > /dev/null 2>&1; }

export PATH="$HOME/.local/bin:$PATH"

export LANG="en_US.UTF-8"
export LC_ALL="$LANG"
export LC_CTYPE="$LANG"

# set up homebrew wherever it may exist

possible_brew_paths=( /usr/local/bin/brew /home/linuxbrew/.linuxbrew/bin/brew $HOME/.linuxbrew/bin/brew /opt/homebrew/bin/brew )
for brewpath in $possible_brew_paths; do
  if exists $brewpath; then
    eval "$($brewpath shellenv)"
    break
  fi
done

if exists brew; then
  brew_prefix_e() { test -e $HOMEBREW_PREFIX/$1 }
else
  # echo "warning: homebrew not present."
  brew_prefix_e() { false }
fi

if [[ -e "/snap/bin" ]]; then
  export PATH="/snap/bin:$PATH"
fi

if brew_prefix_e /share/zsh/site-functions; then
  FPATH=$HOMEBREW_PREFIX/share/zsh/site-functions:$FPATH
fi

if exists nvim; then
  export EDITOR=$(which nvim)
elif exists vim; then
  export EDITOR=$(which vim)
fi
export CLICOLOR=true
export MENU_COMPLETE=false

bindkey -e

HISTSIZE=1000
SAVEHIST=1000
HISTFILE=~/.history
REPORTTIME=10
LISTMAX=0
setopt histignorespace
setopt sharehistory
setopt histexpiredupsfirst

export DOKKU_HOST=dokku.lan

autoload -Uz vcs_info

unamestr=`uname`

if exists zoxide; then
  eval "$(zoxide init zsh)"
elif brew_prefix_e /etc/profile.d/z.sh; then
  . $HOMEBREW_PREFIX/etc/profile.d/z.sh
fi

if [[ "$unamestr" == 'Darwin' ]]; then
  PS1_COLOR="blue"
else
  PS1_COLOR="red"
fi
if [[ "$CODESPACES" == "true" ]]; then
  PS1_COLOR="green"
  PS1_HOSTNAME=" ïƒ‚ $(cat $HOME/.friendly_name)"
else
  PS1_HOSTNAME="%n@%m"
fi
PS1="%F{$PS1_COLOR}$PS1_HOSTNAME%f%(1v.%F{red}%1v%f.-)%# "

# this lets you do things like mmv *.txt *.erb to rename all .txts to .erb
autoload -U zmv
alias mmv="noglob zmv -W"

precmd() {
  psvar=()

  vcs_info
  [[ -n $vcs_info_msg_0_ ]] && psvar[1]="$vcs_info_msg_0_"

  # sets the tab title to current dir
  echo -ne "\e]1;${PWD##*/}\a"
}

RPS1="%F{green}%~%f @ %F{yellow}%D{%H:%M}%f"

autoload -U compinit
compinit
setopt nocorrectall

# Ruby/Rails aliases
alias be='bundle exec'
alias sc="script/console"
alias sg="script/generate"
alias trt="touch tmp/restart.txt"
alias cdp="cap deploy"
alias cdpm="cap deploy:migrations"
alias olm='$EDITOR db/migrate/`ls -t1 db/migrate | head -1`'
alias rdbd='rake db:migrate && RAILS_ENV=test rake db:migrate'
alias migrate="rake db:migrate db:rollback && rake db:migrate"

# Git alises
alias gcd='cd $(git rev-parse --show-cdup).'
alias gs="git status"
alias grh="git reset HEAD"
alias gp="git push"
alias gpl="git pull"
alias gd="git diff"
alias gdc="git diff --cached"
alias ga="git add"
alias gcam="git commit -a -m"
alias gcm="git commit -m"
alias gl="git log --graph --pretty=format:'%Cred%h%Creset -%C(yellow)%d%Creset %s %Cgreen(%cr)%Creset' --abbrev-commit --date=relative"
alias git-undo-commit="git reset --soft 'HEAD^'"
alias whatwasiworkingon="git status --porcelain | cut -c 4- | xargs ls -lct"

# turns "git git ..." into "git ..." for those ditzy days when I type git,
# switch away or think for a sec, switch back to the terminal and type "git" again
git(){ if [ $1 = git ]; then shift; fi; command git "$@"; }

# macOS aliases
alias o="open"
alias flushdns='dscacheutil -flushcache'
alias resetfmtrial="rm ~/Library/Application\ Support/L84577891*"
alias fix-homebrew-ffi='echo "note, this wipes your PKG_CONFIG_PATH + LDFLAGS env vars"; export PKG_CONFIG_PATH="/usr/local/opt/libffi/lib/pkgconfig"; export LDFLAGS="-L/usr/local/opt/libffi/lib"'
alias cd..="cd .."

dash(){ command open dash://$1 }

if ! (( $+commands[tailscale] )); then
  alias tailscale="/Applications/Tailscale.app/Contents/MacOS/Tailscale"
fi

safari-url() {
  osascript -e 'tell application "Safari" to get URL of current tab of front window'
}

get-sfmono() {
  if [ -d /Applications/Utilities/Terminal.app ]; then
    TERMINALDIR=/Applications/Utilities/Terminal.app
  elif [ -d /System/Applications/Utilities/Terminal.app ]; then
    TERMINALDIR=/System/Applications/Utilities/Terminal.app
  else
    echo "Couldn't find a Terminal.app, bailing"
    return 1
  fi

  cp -R $TERMINALDIR/Contents/Resources/Fonts/* ~/Library/Fonts
  echo "Copied SF Mono from $TERMINALDIR"
}

# general aliases
alias findswaps="find . -name '*.swp'"
alias recoverswaps="find . -name '*.swp' -exec $EDITOR -r -c DiffSaved {} \;"

alias setup-ssh-agent='eval "$(ssh-agent)" && ssh-add'
alias ssh-pw="ssh -o PubKeyAuthentication=no"
alias yt-dlp-mp4='yt-dlp -f "bestvideo[ext=mp4]+bestaudio[ext=m4a]/best[ext=mp4]"'

reverselookupdns(){ command dig $1 +short | xargs -J % dig -x % +short }

whatport() {
  lsof -i :$1
}

if exists fdfind; then
  alias fd="fdfind"
fi

if exists batcat; then
  alias bat="batcat"
  BATCMD="batcat"
fi

if exists bat; then
  : "${BATCMD:=bat}"
  export PAGER="$BATCMD -p"
  export BAT_PAGER="less"
  export MANPAGER="sh -c 'col -bx | $BATCMD -l man -p'"
  export MANROFFOPT="-c"
  export BAT_THEME="Monokai Extended"
  alias bathelp='$BATCMD --plain --language=help'
  help() {
    "$@" --help 2>&1 | bathelp
  }
fi

test-truecolor() {
  awk 'BEGIN{
      s="/\\/\\/\\/\\/\\"; s=s s s s s s s s;
      for (colnum = 0; colnum<77; colnum++) {
          r = 255-(colnum*255/76);
          g = (colnum*510/76);
          b = (colnum*255/76);
          if (g>255) g = 510-g;
          printf "\033[48;2;%d;%d;%dm", r,g,b;
          printf "\033[38;2;%d;%d;%dm", 255-r,255-g,255-b;
          printf "%s\033[0m", substr(s,colnum+1,1);
      }
      printf "\n";
  }'
  echo "Colors should be smooth and not banded"
}

256-color-codes() {
  for i in {0..255} ; do
    printf "\x1b[38;5;${i}m${i} "
  done
}

_rake_does_task_list_need_generating () {
  if [ ! -f .rake_tasks ]; then return 0;
  else
    accurate=$(stat -f%m .rake_tasks)
    changed=$(stat -f%m Rakefile)
    return $(expr $accurate '>=' $changed)
  fi
}

_rake () {
  if [ -f Rakefile ]; then
    if _rake_does_task_list_need_generating; then
      echo "\nGenerating .rake_tasks..." > /dev/stderr
      bundle exec rake --silent --tasks | cut -d " " -f 2 > .rake_tasks
    fi
    compadd `cat .rake_tasks`
  fi
}

compdef _rake rake

if exists hub; then
  eval "$(hub alias -s)" # activate hub helper script
fi

unalias run-help &> /dev/null
autoload run-help

source "${HOME}/.iterm2_shell_integration.zsh"

# enables Erlang history
export ERL_AFLAGS="-kernel shell_history enabled"

if exists rbenv; then
  eval "$(rbenv init -)"
fi

if exists nodenv; then
  eval "$(nodenv init -)"
fi

if exists pyenv; then
  export PYENV_ROOT="$HOME/.pyenv"
  export PATH="$PYENV_ROOT/bin:$PATH"
  eval "$(pyenv init --path)"
  eval "$(pyenv init -)"
fi

if exists mise; then
  eval "$(mise activate zsh)"
fi

# fzf via Homebrew
if brew_prefix_e /opt/fzf/shell/completion.zsh && brew_prefix_e /opt/fzf/shell/key-bindings.zsh; then
  source $HOMEBREW_PREFIX/opt/fzf/shell/key-bindings.zsh
  source $HOMEBREW_PREFIX/opt/fzf/shell/completion.zsh
elif [ -e /usr/share/fzf/key-bindings.zsh ] && [ -e /usr/share/fzf/completion.zsh ]; then
  source /usr/share/fzf/key-bindings.zsh
  source /usr/share/fzf/completion.zsh
elif [ -e /usr/share/doc/fzf/examples/key-bindings.zsh ] && [ -e /usr/share/doc/fzf/examples/completion.zsh ]; then
  source /usr/share/doc/fzf/examples/key-bindings.zsh
  source /usr/share/doc/fzf/examples/completion.zsh
fi

# fzf configuration
if exists fzf; then
  if exists fd; then
    export FZF_DEFAULT_COMMAND='fd --type file --color=always'
  elif exists rg; then
    export FZF_DEFAULT_COMMAND='rg --color always -g "" --files'
  fi

  if (( ${+FZF_DEFAULT_COMMAND} )); then
    export FZF_DEFAULT_OPTS="--ansi"
    export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"
    export FZF_ALT_C_COMMAND="$FZF_DEFAULT_COMMAND"
  fi
fi

if brew_prefix_e /share/zsh-autosuggestions/zsh-autosuggestions.zsh; then
  source $HOMEBREW_PREFIX/share/zsh-autosuggestions/zsh-autosuggestions.zsh
fi

export VAULT_URL="none"

if brew_prefix_e /share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh; then
  source $HOMEBREW_PREFIX/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
fi

declare -A replacements
replacements=( ls exa cat bat )

if exists eza; then
  replacements[ls]=eza
fi

for command replacement in ${(kv)replacements}; do
  if exists $replacement; then
    alias $command=$replacement
  else
    echo "$replacement doesn't exist, using standard $command"
  fi
done

# Base16 Shell
BASE16_SHELL="$HOME/.config/base16-shell/"
[ -n "$PS1" ] && \
    [ -s "$BASE16_SHELL/profile_helper.sh" ] && \
        eval "$("$BASE16_SHELL/profile_helper.sh")"

ports() {
    (
        echo 'PROC PID USER x IPV x x PROTO BIND PORT'
        (
            sudo lsof +c 15 -iTCP -sTCP:LISTEN -P -n | tail -n +2
            sudo lsof +c 15 -iUDP -P -n | tail -n +2 | egrep -v ' (127\.0\.0\.1|\[::1\]):'
        ) | sed -E 's/ ([^ ]+):/ \1 /' | sort -k8,8 -k5,5 -k1,1 -k10,10n
    ) | awk '{ printf "%-16s %-6s %-9s %-5s %-7s %s:%s\n",$1,$2,$3,$5,$8,$9,$10 }'
}

# Created by `pipx` on 2023-06-09 18:30:59
export PATH="$PATH:/Users/rufo/.local/bin"

export GOPATH="$GOPATH:$HOME/.local/share/go"

if [ $(ps ax | grep "[s]sh-agent" | wc -l) -eq 0 ] ; then
    eval $(ssh-agent -s) > /dev/null
    if [ "$(ssh-add -l)" = "The agent has no identities." ] ; then
       if [[ "$unamestr" == 'Darwin' ]]; then
         ssh-add --apple-load-keychain > /dev/null 2>&1
       fi
        # Auto-add ssh keys to your ssh agent
        # Example:
        # ssh-add ~/.ssh/id_rsa > /dev/null 2>&1
    fi
fi

if exists atuin; then
  eval "$(atuin init zsh)"
elif [[ -e "$HOME/.atuin/bin/env" ]]; then
   . "$HOME/.atuin/bin/env"
else
  echo "note: atuin not found"
fi

if [[ -e "$HOME/.cache/lm-studio/bin" ]]; then
  export PATH="$PATH:$HOME/.cache/lm-studio/bin"
fi
